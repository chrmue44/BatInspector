# intelligent transfer of automatically recognized species to SpeciesMan

#step 1: kick out all files overdriven calls
#take over all unambiguously automatically recognized species
SET NrFiles =(getPrjFileCount() - 1)
SET Count 0
FOR FileNr 0 NrFiles
  IF =(getFileInfo(FileNr, "SELECT"))
    SET NrCalls =(getCallCount(FileNr) - 1)
    FOR i 0 NrCalls
	  SET OverDr =(checkOverdrive(FileNr, i))
	  IF =(OverDr)
   	    SET specMan =(getCallInfo(FileNr, i, "SPEC_MAN"))
	    IF =(toupper(specMan) == "TODO")
          =(setCallInfo(FileNr, i, "SPEC_MAN", "?"))
  	      SET fileName =(getFileInfo(FileNr, "NAME"))
		  SET Count =(Count + 1)
          LOG =("file:" + fileName + ", overdriven call:" + (i + 1) + " set to '?')		  
		END
	  END
	END
  END
END
LOG =("detected " + Count + " overdriven calls")

#step 2: handle all files with mostly one species (kickout unplausible results)
#take over all unambiguously automatically recognized species
SET NrFiles =(getPrjFileCount() - 1)
SET Count 0
FOR FileNr 0 NrFiles
  IF =(getFileInfo(FileNr, "SELECT"))
    SET NrCalls =(getCallCount(FileNr) - 1)
    SET NrOfSpecies =(getNrOfSpecies(FileNr))
    SET Replace 0
    IF =(NrOfSpecies >= 1)
      SET Rank1Spec =(getRankSpecies(FileNr, 1))
      SET Rank1Cnt =(getRankCount(FileNr, 1))
      IF =(NrOfSpecies >= 2)
        SET Rank2Spec =(getRankSpecies(FileNr, 2))
        SET Rank2Cnt =(getRankCount(FileNr, 2))
        IF =((Rank2Cnt < 2) && (Rank1Cnt > 3))
	      SET Replace 1
	    END
  	  ELSE
	    SET Replace 1
      END
	  IF =(Replace == 1)
	    SET Count =(Count + 1)
        FOR i 0 NrCalls
	      SET specAuto =(getCallInfo(FileNr, i, "SPEC_AUTO"))
		  SET specMan =(getCallInfo(FileNr, i, "SPEC_MAN"))
		  IF =(toupper(specMan) == "TODO")
		    IF =(specAuto == Rank1Spec)
              =(setCallInfo(FileNr, i, "SPEC_MAN", Rank1Spec))
		    ELSE
              =(setCallInfo(FileNr, i, "SPEC_MAN", "?"))
            END
		  END
	    END
	  END
    END
  END
END
LOG =("replace SpeciesMan in " + Count + " files")

#step 3: handle the rest
#take over all unambiguously automatically recognized species
SET NrFiles =(getPrjFileCount() - 1)
SET Count 0
SET Replace 0
FOR FileNr 0 NrFiles
  IF =(getFileInfo(FileNr, "SELECT"))
    SET NrCalls =(getCallCount(FileNr) - 1)
    FOR i 0 NrCalls
	  SET specAuto =(getCallInfo(FileNr, i, "SPEC_AUTO"))
      SET specMan =(getCallInfo(FileNr, i, "SPEC_MAN"))
	  SET prob =(getCallInfo(FileNr, i, "PROBABILITY"))
	  IF =(toupper(specMan) == "TODO")
	    SET Replace 1
	    IF =(prob > 0.5)
          =(setCallInfo(FileNr, i, "SPEC_MAN", specAuto))
	    ELSE
          =(setCallInfo(FileNr, i, "SPEC_MAN", "?"))
	    END
	  END
    END
	IF =(Replace == 1)
	  SET Replace 0
	  SET Count =(Count + 1)
	END
  END
END
LOG =("SpecMan replaced in " + Count + " files")
