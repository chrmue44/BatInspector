; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#include "CodeDependencies.iss"

#define MyAppName "BatInspector"
#define MyInstallerName "SetupBatInspector_0943"
#define MyAppVersion "0.9.4.3"
#define MyAppPublisher "Christian Müller"
#define MyAppURL "https://www.example.com/"
#define MyAppExeName "BatInspector.exe"
#define MyAppDataFolder "{commonappdata}\BatInspector"
#define SourceFolder "F:\prj\BatInspector"
#define PythonInstaller "python-3.11.0-amd64.exe"
#define PythonInstFolder "python311"
#define BatDetectVersion "1.1.1"
#define BbnetVersion "b8b9cd8a89980d7e71d4bce74d304c2ebbc20433"
#define BirdnetVersion "0b70738ae838cd0b62d06b81ea467789a0132741"


[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{5F3F5760-EF7F-423E-9DB3-1797D9922239}
AppName=BatInspector
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={commonpf64}\{#MyAppName}
DisableProgramGroupPage=yes
DisableWelcomePage=no
DisableDirPage=no
LicenseFile=F:\prj\{#MyAppName}\LICENSE
; Uncomment the following line to run in non administrative install mode (install for current user only.)
;PrivilegesRequired=lowest
OutputDir={#SourceFolder}\InnoSetup\bin
OutputBaseFilename={#MyInstallerName}
Compression=lzma
SolidCompression=yes
AlwaysShowDirOnReadyPage=yes
AlwaysShowComponentsList=yes
WizardStyle=modern
SetupIconFile={#SourceFolder}\{#MyAppName}\images\kopf64.ico
UninstallDisplayIcon={#SourceFolder}\{#MyAppName}\images\kopf64.ico
ArchitecturesInstallIn64BitMode=x64

[Types]
Name: "full"; Description: "{cm:msgInstFull}"
Name: "batsBirds"; Description: "{cm:msgBatsBirds}"
Name: "bats"; Description: "{cm:msgInstBats}"
Name: "batsOnlyBd2"; Description: "{cm:msgInstBatBd2}"
Name: "birds"; Description: "{cm:msgInstBirds}"
Name: "custom"; Description: "{cm:msgInstCustom}"; Flags: iscustom

[Components]
Name: "modbd2"; Description: "{cm:cmpBd2}"; Types: full bats batsBirds batsOnlyBd2; ExtraDiskSpaceRequired:5986975744;  Flags: dontinheritcheck
Name: "modbatty"; Description: "{cm:cmpBbnet}"; Types: full bats; ExtraDiskSpaceRequired: 4960681984;  Flags: dontinheritcheck
Name: "modbnet"; Description: "{cm:cmpBirdNet}"; Types: full batsBirds birds; ExtraDiskSpaceRequired: 6827147264;  Flags: dontinheritcheck
Name: "python"; Description: "{cm:cmpPython}"; Types: full bats batsBirds batsOnlyBd2 birds; ExtraDiskSpaceRequired:97954967;  Flags: dontinheritcheck

[Languages]
Name: "en"; MessagesFile: "English.isl"
Name: "de"; MessagesFile: "German.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; 
Name: "starticon"; Description: "{cm:CreateStartIcon}"; GroupDescription: "{cm:AdditionalIcons}"; 

[Files]
Source: "{#SourceFolder}\{#MyAppName}\bin\Release\{#MyAppExeName}"; DestDir: "{app}"; Check: Dependency_IsX64; Flags: ignoreversion
Source: "{#SourceFolder}\{#MyAppName}\datapath.txt"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#SourceFolder}\{#MyAppName}\bin\Release\teensy_loader_cli.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#SourceFolder}\{#MyAppName}\bin\Release\*.dll"; DestDir: "{app}"; Check: Dependency_IsX64; Flags: ignoreversion
Source: "{#SourceFolder}\{#MyAppName}\bin\Release\*.pdb"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#SourceFolder}\{#MyAppName}\bin\Release\*.xml"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#SourceFolder}\{#MyAppName}\bin\Release\*.txt"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#SourceFolder}\{#MyAppName}\bin\Release\de\*"; DestDir: "{app}\de"; Flags: ignoreversion
Source: "{#SourceFolder}\{#MyAppName}\bin\Release\*.config"; DestDir: "{app}"; Flags: ignoreversion
source: "{#SourceFolder}\{#MyAppName}\dat\*.wav"; DestDir: "{#MyAppDataFolder}\dat";
source: "{#SourceFolder}\{#MyAppName}\dat\*.md"; DestDir: "{#MyAppDataFolder}\dat";
source: "{#SourceFolder}\{#MyAppName}\dat\*.xml"; DestDir: "{#MyAppDataFolder}\dat";
source: "{#SourceFolder}\{#MyAppName}\dat\batInfo.json"; DestDir: "{#MyAppDataFolder}\setup"; Permissions: everyone-full
source: "{#SourceFolder}\{#MyAppName}\doc\*"; DestDir: "{app}\doc";
source: "{#SourceFolder}\{#MyAppName}\scripts\*"; DestDir: "{#MyAppDataFolder}\setup\scripts"; Permissions: everyone-full
source: "{#SourceFolder}\{#MyAppName}\models\bd2\*"; DestDir: "{#MyAppDataFolder}\models\bd2"; Components: modbd2
source: "{#SourceFolder}\{#MyAppName}\models\bd2\batdetect2\models\*"; DestDir: "{#MyAppDataFolder}\models\bd2\batdetect2\models\"; Components: modbd2
source: "{#SourceFolder}\{#MyAppName}\models\bbnet\*"; DestDir: "{#MyAppDataFolder}\models\bbnet"; Components: modbatty
source: "{#SourceFolder}\{#MyAppName}\models\birdnet\*"; DestDir: "{#MyAppDataFolder}\models\birdnet"; Components: modbnet
source: "{#SourceFolder}\InnoSetup\python\*"; DestDir: "{#MyAppDataFolder}\setup"; Permissions: everyone-full; Components: python
source: "{#SourceFolder}\BatInspector\images\kopf64.ico"; DestDir: "{app}";

[Dirs]
Name: "{#MyAppDataFolder}\log"; Permissions: everyone-full

; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{autoprograms}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; IconFilename: "{app}\kopf64.ico" ;
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; IconFilename: "{app}\kopf64.ico"; Tasks: desktopicon starticon

[Run]
Filename: "{#MyAppDataFolder}\setup\install_python.bat"; Parameters:"""{app}\{#PythonInstFolder}"" {#PythonInstaller} """; Flags:runasoriginaluser; Components: python
Filename: "{#MyAppDataFolder}\setup\install_bd2.bat"; Parameters:"""{app}\{#PythonInstFolder}"" {#BatDetectVersion}"; Flags:runasoriginaluser; Components: modbd2
Filename: "{#MyAppDataFolder}\setup\install_bbnet.bat"; Parameters:"""{app}\{#PythonInstFolder}"" {#BbnetVersion}"; Flags:runasoriginaluser; Components: modbatty
Filename: "{#MyAppDataFolder}\setup\install_birdnet.bat"; Parameters:"""{app}\{#PythonInstFolder}"" {#BirdnetVersion}"; Flags:runasoriginaluser; Components: modbnet
; Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall unchecked; StatusMsg: {cm:MsgInstModell} 

[UninstallDelete]
Type: files; Name: "{app}\{#MyAppExeName}";
Type: files; Name: "{app}\*.dll";
Type: files; Name: "{app}\*.xml"; 
Type: files; Name: "{app}\*.txt"; 
Type: files; Name: "{app}\de\*"; 
Type: files; Name: "{app}\*.config"; 
Type: filesandordirs; Name: "{#MyAppDataFolder}";
Type: filesandordirs; Name: "{app}";

[CustomMessages]
de.CreateStartIcon="Verknüpfung im Startmenü erstellen"
de.MsgInstModell="Installation des KI-Modells. Dieser Vorgang dauert einige Minuten, bitte Geduld!..."
en.CreateStartIcon="create entry in start menu"
en.MsgInstModell="Installing AI model. This will take several minutes, be patient!..."

[UninstallRun]
Filename: "{#MyAppDataFolder}\setup\uninstall_python.bat"; Parameters:"{#PythonInstaller}"; RunOnceId: "DelService"; Components: python

[Code]
function InitializeSetup: Boolean;
begin
  // comment out functions to disable installing them
  Dependency_AddDotNet47;
  Dependency_AddVC2015To2022;
  Result := True;
end;
